extension_inc = include_directories('../include', '../librb/include')
extension_link_args = []
extension_c_args = []

if cc.has_link_argument('-export-symbols-regex')
  extension_link_args += ['-Wl,-export-symbols-regex,_mheader']
endif

extension_modules = [
  'cap_oper',
  'cap_realhost',
  'chantype_dummy',
  'chm_adminonly',
  'chm_operonly',
  'chm_insecure',
  'chm_nonotice',
  'chm_operpeace',
  'chm_regmsg',
  'chm_sslonly',
  'createauthonly',
  'createoperonly',
  'drain',
  'extb_account',
  'extb_canjoin',
  'extb_channel',
  'extb_guest',
  'extb_hostmask',
  'extb_oper',
  'extb_server',
  'extb_ssl',
  'extb_realname',
  'extb_usermode',
  'extb_extgecos',
  'extb_combi',
  'force_user_invis',
  'helpops',
  'hide_uncommon_channels',
  'hurt',
  'identify_msg',
  'invex_regonly',
  'invite_notify',
  'ip_cloaking',
  'ip_cloaking_old',
  'ip_cloaking_3.0',
  'ip_cloaking_4.0',
  'm_adminwall',
  'm_echotags',
  'm_extendchans',
  'm_findforwards',
  'm_identify',
  'm_locops',
  'm_mkpasswd',
  'm_ojoin',
  'm_okick',
  'm_omode',
  'm_opme',
  'm_remove',
  'm_sendbans',
  'm_shedding',
  'm_webirc',
  'no_kill_services',
  'no_locops',
  'no_oper_invis',
  'override',
  'override_kick_immunity',
  'restrict-unauthenticated',
  'sasl_usercloak',
  'sno_channelcreate',
  'sno_farconnect',
  'sno_globalnickchange',
  'sno_globaloper',
  'tag_message_id',
  'tag_typing',
  'umode_hide_idle_time',
  'umode_noctcp',
  'example_module',
]

foreach mod : extension_modules
  shared_module(mod,
    mod + '.c',
    name_prefix: '',
    name_suffix: 'so',
    dependencies: [libircd_dep, librb_dep],
    include_directories: extension_inc,
    link_args: extension_link_args,
    c_args: extension_c_args,
    install: true,
    install_dir: moduledir / 'extensions'
  )
endforeach

if have_hyperscan
  shared_module('filter',
    'filter.c',
    name_prefix: '',
    name_suffix: 'so',
    dependencies: [libircd_dep, librb_dep, hyperscan_dep],
    include_directories: extension_inc,
    link_args: extension_link_args,
    c_args: extension_c_args,
    install: true,
    install_dir: moduledir / 'extensions'
  )
endif
